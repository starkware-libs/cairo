= Contracts

Starknet contracts can be written in Cairo.

== Example

[source,rust]
----
#[starknet::contract]
mod my_contract {
    struct Storage {
        x: felt252,
        m: LegacyMap::<felt252, u128>,
    }

    #[external(v0)]
    fn foo(value: felt252) -> felt252 {
        let y = x::read() + value;
        m::write(y, 3_u128);
        y
    }
}
----

A contract is written as an inline module, with designated attributes.

The main attribute is the `#[starknet::contract]` attribute that makes the starknet plugin treat the
module as a contract.

== Storage


Unlike a Cairo program, which is stateless, a Starknet contract has a state, called "the contractâ€™s
storage".
Transactions invoked on such contracts may modify this state.

In a contract, you can define a struct named `Storage`. The members of this struct are the storage
variables.
The storage variables may be of any Cairo type that implements the `starknet::Store` trait, and
legacy mappings that store key-value pairs.

Accessing a storage member is done using the `::read()` and `::write(value)` functions which are
automatically created by the Starknet plugin.
For example, for the storage member `x` above, access is done using `x::read()` and
`x::write(value)`.

Defining a mapping storage member is done using the `LegacyMap` "type" that is expanded by
the Starknet plugin. The keys must implement the `Hash` trait and the values must implement the
`starknet::Store` trait.
Accessing such storage members is done using the `::read(key)` and
`::write(key, value)` functions which are also automatically created by the Starknet plugin.
For example, for the storage member `m` above, access is done using `m::read(key)` and
`m::write(key, value)`.

When a contract is deployed, all of its storage variables are default-initialized
(deserialized from zeroes).

== Entry points

Starknet contracts have no main() function. Instead, each function may be annotated as a special
function using one of the attributes below:

- `#[external(v0)]` functions may be called by Starknet users or by other contracts. For example:
[source]
----
    #[external(v0)]
    fn external_example(ref self: ContractState, value: felt252) {
        x::write(value);
    }
----
Note the first parameter. It represents the state of the contract storage.
The `ref` modifier means that the state may change - by writing to storage, for example.
Another possibility is to get a read-only reference to the storage:
[source]
----
    #[external(v0)]
    fn view_example(self: @ContractState) -> felt252 {
        x::read(value)
    }
----
This is a view function - it can only read from the storage.
- `#[constructor]` function is called when the contract is deployed. There may be only one such
function and it must be called `constructor`. If it is not defined, the contract is deployed with
all storage variables default-initialized. For example:
[source]
----
    #[constructor]
    fn constructor() {
        x::write(3);
    }
----
- `#[l1_handler]` functions are called when the contract receives a message from L1. For example:
[source]
----
    #[l1_handler]
    fn l1_handler_example(from_address: felt252) {
        assert(from_address == 0x5678, 'EXPECTED_0x5678');
        x::write(5);
    }
----
- Functions without any of the above attributes are private functions, and may only be called by
other functions in the same contract. They are not entry points. For example:
[source]
----
    fn private_helper_add(a: felt252, b: felt252) -> felt252 {
        a + b
    }
----

== Events

Contract events trigger events on Starknet. They can be triggered by the contract.

The are defined using the `#[event]` attribute on an empty function.
For example:
[source]
----
    #[event]
    fn event_example(event_data: felt252) {}
----

== ABI

Each contract has an ABI (Application Binary Interface) that defines:

- The entry points that can be called on it, of the kinds mentioned above.
- The types in the entry points signatures.
- The contract's events.


== Calling Other Contracts

The Starknet plugin generates a contract interface for each defined contract.
It is a trait that can be found in `<contract_name>::__abi`.
You can also write a contract interface manually, for contracts that are not implemented in
your code.

For example, the contract interface for `my_contract` above can be manually written as:
[source,rust]
----
#[starknet::interface]
trait IMyContract {
    fn foo(value: felt252) -> felt252;
}
----

=== Using the Contract Interface Dispatcher

For each contract interface, 2 dispatchers are automatically created and exported:
a contract-dispatcher and a library-dispatcher.
That is, for every contract you implement or contract interface you manually add.

You can use another contract interface contract-dispatcher to call another contract
in the following way:

[source,rust]
----
#[starknet::interface]
trait IMyContract<TContractState> {
    fn foo(ref self: TContractState, value: felt252) -> felt252;
}

#[starknet::contract]
mod my_second_contract {
    use super::IMyContractDispatcherTrait;
    use super::IMyContractDispatcher;

    #[external(v0)]
    fn call_foo(
        another_contract_address: starknet::ContractAddress,
        a: felt252
    ) -> felt252 {
        let mut dispatcher = IMyContractDispatcher { contract_address: another_contract_address };
        dispatcher.foo(a)
    }
}
----

This will run the other contract's logic in its context, and may, for example, change its state.

You can also call a function from another contract class as a library function.
This means the function's logic is executed from the caller contract's context.
This can be done using the library-dispatcher in the following way:

[source,rust]
----
#[starknet::interface]
trait IMyContract {
    fn foo(value: felt252);
}

#[starknet::contract]
mod my_second_contract {
    use super::IMyContractDispatcherTrait;
    use super::IMyContractLibraryDispatcher;

    #[external(v0)]
    fn libcall_foo(a: felt252) -> felt252 {
        IMyContractLibraryDispatcher { class_hash: starknet::class_hash_const::<0x1234>() }.foo(a)
    }
}
----

Where `0x1234` is the `ClassHash` of the contract class implementing `foo`.

=== Using system calls

Another way to call another contract is to use the `starknet::syscalls::call_contract_syscall`
system call. The dispatcher described above is the high-level syntax for the low-level system calls.
Thus, prefer using the dispatcher method.
You can use the system call directly if you want to have customized error handling or more
control over the serialization/deserialization of the call data and the returned data.

To directly call another contract using `starknet::syscalls::call_contract_syscall` you can do the
following, but the result is the serialized return value of the function which you need to
deserialize yourself.
You also need to compute the selector of the function you want to call, which is the
keccak hash of the function name - in this case `starknet_keccak("foo")`.

[source,rust]
----
#[starknet::contract]
mod my_second_contract {
    #[external(v0)]
    fn syscall_call_another_contract(
        address: starknet::ContractAddress, selector: felt252, calldata: Array<felt252>
    ) -> Span::<felt252> {
        starknet::syscalls::call_contract_syscall(
            :address, entry_point_selector: selector, calldata: calldata.span()
        ).unwrap_syscall()
    }
}
----

// TODO(yuval): add links to compilation, abi format, declaring/deployment from hello_starknet.
