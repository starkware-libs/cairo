= Type and impl aliases

A type alias is a new name for an existing type. It is not a new type. This means that the alias and
the original type are treated as the same type by the compiler and can be used interchangeably.
Type aliases are created with the type keyword:
[source,cairo]
----
type NewName<Generics> = ConcreteType;
----

Examples:
[source,cairo]
----
type IntAlias = i32;
type BoxIntAlias = Box<i32>;
type BoxOption<T> = Box<Option<T>>;
----

An impl alias is similar to a type alias, but for impls.
An impl alias is a new name for an existing impl. It is not a new impl. This means that the alias
refers to the same implementation and can be used wherever the original impl is expected. Impl
aliases are created with the impl keyword:
[source,cairo]
----
impl NewName<Generics> = ConcreteImpl;
----

Examples:
[source,cairo]
----
trait Pow<T> {
    fn pow(base: T, exp: u32) -> T;
}

impl AnyAlgebraPow<T, impl AlgImpl: Algebra<T>> of Pow<T> {
    fn pow(base: T, exp: u32) -> T {
        // Implementation details.
        base
    }
}

trait Algebra<T> {
    fn identity() -> T;
}

impl I32Algebra of Algebra<i32> {
    fn identity() -> i32 {
        1
    }
}

// Impl alias for Pow of i32.
impl Int32Pow = AnyAlgebraPow<i32, I32Algebra>;
----
