// Simple parser for sierra.
// Currently only used for testing.

use crate::ids::*;
use crate::program::*;
use num_bigint::BigInt;

grammar;

pub Program: Program = {
    <type_declarations:TypeDeclaration*>
    <libfunc_declarations:LibFuncDeclaration*>
    <statements:Statement*>
    <funcs:Function*>
    => Program{type_declarations, libfunc_declarations, statements, funcs},
}

TypeDeclaration: TypeDeclaration = {
    "type" <id:ConcreteTypeId> "=" <long_id:ConcreteTypeLongId> ";" => TypeDeclaration{id, long_id},
}

pub ConcreteTypeLongId: ConcreteTypeLongId = {
    <generic_id:GenericTypeId> "<" <generic_args:GenericArgs> ">"
    => ConcreteTypeLongId{generic_id, generic_args},
    <generic_id:GenericTypeId> => ConcreteTypeLongId{generic_id, generic_args: vec![]},
}

LibFuncDeclaration: LibFuncDeclaration = {
    "libfunc" <id:ConcreteLibFuncId> "=" <long_id:ConcreteLibFuncLongId> ";"
    => LibFuncDeclaration{id, long_id},
}

pub ConcreteLibFuncLongId: ConcreteLibFuncLongId = {
    <generic_id:GenericLibFuncId> "<" <generic_args:GenericArgs> ">"
    => ConcreteLibFuncLongId{generic_id, generic_args},
    <generic_id:GenericLibFuncId> => ConcreteLibFuncLongId{generic_id, generic_args: vec![]},
}

Function: Function = {
    <id:FunctionId> "@" <entry:StatementIdx>
    "(" <params: Params> ")" "->" "(" <ret_types: ConcreteTypeIds> ")" ";"
    => Function::new(id, params, ret_types, entry),
}

Param: Param = {
    <id:VarId> ":" <ty:ConcreteTypeId> => Param{id, ty},
}
Params = Comma<Param>;

GenericLibFuncId: GenericLibFuncId = {
    <id:PathLabel> => GenericLibFuncId::from_string(id),
    "[" <id:UnsignedInt> "]" => GenericLibFuncId::new(id),
}

ConcreteLibFuncId: ConcreteLibFuncId = {
    <id:ConcreteLabel> => ConcreteLibFuncId::from_string(id),
    "[" <id:UnsignedInt> "]" => ConcreteLibFuncId::new(id),
}

FunctionId: FunctionId = {
    <id:ConcreteLabel> => FunctionId::from_string(id),
    "[" <id:UnsignedInt> "]" => FunctionId::new(id),
}

UserTypeId: UserTypeId = {
    <id:ConcreteLabel> => UserTypeId::from_string(id),
    "[" <id:UnsignedInt> "]" => UserTypeId::new(id),
}

VarId: VarId = {
    <id:BasicLabel> => VarId::from_string(id),
    "[" <id:UnsignedInt> "]" => VarId::new(id),
}
VarIds = Comma<VarId>;

GenericTypeId: GenericTypeId = {
    <id:PathLabel> => GenericTypeId::from_string(id),
    "[" <id:UnsignedInt> "]" => GenericTypeId::new(id),
}

ConcreteTypeId: ConcreteTypeId = {
    <id:ConcreteLabel> => ConcreteTypeId::from_string(id),
    "[" <id:UnsignedInt> "]" => ConcreteTypeId::new(id),
}
ConcreteTypeIds = Comma<ConcreteTypeId>;

StatementIdx: StatementIdx = {
     <id:UnsignedInt> => StatementIdx(id as usize),
}

GenericArg: GenericArg = {
    ConcreteTypeId => GenericArg::Type(<>),
    <v:BigInt> => GenericArg::Value(v),
    "user@" <id:FunctionId> => GenericArg::UserFunc(id),
    "ut@" <id:UserTypeId> => GenericArg::UserType(id),
    "lib@" <id:ConcreteLibFuncId> => GenericArg::LibFunc(id),
}
GenericArgs = Comma<GenericArg>;

Statement: Statement = {
    <invocation:Invocation> => Statement::Invocation(invocation),
    "return" "(" <args:VarIds> ")" ";" => Statement::Return(args),
}

Invocation: Invocation = {
    <libfunc_id:ConcreteLibFuncId> "(" <args:VarIds> ")" "->" "(" <results:VarIds> ")" ";" =>
        Invocation{
            libfunc_id,
            args,
            branches: vec![BranchInfo{ target: BranchTarget::Fallthrough , results }]
        },
    <libfunc_id:ConcreteLibFuncId> "(" <args:VarIds> ")" "{" <branches:BranchInfo*> "}" ";" =>
        Invocation{libfunc_id, args, branches},
}

// Generates a vector of Ts with or without a trailing comma.
Comma<T>: Vec<T> = {
    // If we have an additional element we add it to the vector, otherwise
    // returning the current vector.
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

BranchInfo: BranchInfo = {
    <target: BranchTarget> "(" <results:VarIds> ")" =>
        BranchInfo{ target, results },
}

BranchTarget: BranchTarget = {
     "fallthrough" => BranchTarget::Fallthrough,
     <id:StatementIdx> => BranchTarget::Statement(id),
}

BasicLabel: String = {
    r"[a-zA-Z_][a-zA-Z_0-9]*" => <>.to_string(),
}

// `BasicLabel`s joined by "::".
PathLabel: String = {
    BasicLabel => <>,
    <path_label:PathLabel> "::" <basic_label:BasicLabel> => format!("{path_label}::{basic_label}"),
}

// `GenericArg`s joined by ",".
GenericArgsString: String = {
    GenericArg => <>.to_string(),
    <head:GenericArgsString> "," <tail:GenericArg> => format!("{head}, {tail}"),
}

// Label that can also include generic arguments.
ConcreteLabel: String = {
    <base:PathLabel> "::" "<" <generic_args:GenericArgsString> ">" => format!("{base}::<{generic_args}>"),
    <base:PathLabel> "<" <generic_args:GenericArgsString> ">" => format!("{base}<{generic_args}>"),
    "(" <generic_args:GenericArgsString> ")" => format!("({generic_args})"),
    "(" ")" => format!("()"),
    PathLabel => <>,
}

BigInt: BigInt = {
    r"-?[1-9][0-9]*|0" => <>.parse().unwrap(),
}

UnsignedInt: u64 = {
    BigInt => u64::try_from(<>).unwrap(),
}

SignedInt: i64 = {
    BigInt => i64::try_from(<>).unwrap(),
}

match {
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]" => {},
    _,
}
