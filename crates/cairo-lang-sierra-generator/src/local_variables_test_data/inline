//! > inlining of a function with a local var.

//! > test_runner_name
check_find_local_variables

//! > function_name
foo

//! > function_code
fn foo(x: felt252) -> felt252 {
    bar(x)
}

//! > module_code
#[inline(always)]
fn bar(x: felt252) -> felt252 {
    if x == 0 {
        return x + 1;
    } else {
        let a = x + x;
        // The call will force `a` to be a local variable.
        revoke_ap();
        a
    }
}

// Revokes ap since this function is recursive.
fn revoke_ap() {
    revoke_ap()
}

//! > lowering_format
Parameters: v0: core::felt252
blk0 (root):
Statements:
End:
  Match(match core::felt252_is_zero(v0) {
    IsZeroResult::Zero => blk1,
    IsZeroResult::NonZero(v18) => blk2,
  })

blk1:
Statements:
  (v10: core::felt252) <- 1u
  (v24: core::felt252) <- core::felt252_add(v0, v10)
End:
  Return(v24)

blk2:
Statements:
  (v25: core::felt252) <- core::felt252_add(v0, v0)
  (v13: ()) <- test::revoke_ap()
End:
  Return(v25)

//! > local_variables
v25

//! > ==========================================================================

//! > inlining of a function with a revoking goto.

//! > test_runner_name
check_find_local_variables

//! > function_name
foo

//! > function_code
fn foo(x: felt252) -> felt252 {
    bar(x) + x
}

//! > module_code
#[inline(always)]
fn bar(x: felt252) -> felt252 {
    if x == 0 {
        revoke_ap();
        return x + 1;
    } else {
        let a = x + x;
        // The call will force `a` to be a local variable.
        revoke_ap();
        a
    }
}

// Revokes ap since this function is recursive.
fn revoke_ap() {
  revoke_ap()
}

//! > lowering_format
Parameters: v0: core::felt252
blk0 (root):
Statements:
End:
  Match(match core::felt252_is_zero(v0) {
    IsZeroResult::Zero => blk1,
    IsZeroResult::NonZero(v21) => blk2,
  })

blk1:
Statements:
  (v11: ()) <- test::revoke_ap()
  (v12: core::felt252) <- 1u
  (v27: core::felt252) <- core::felt252_add(v0, v12)
End:
  Goto(blk3, {v27 -> v1})

blk2:
Statements:
  (v28: core::felt252) <- core::felt252_add(v0, v0)
  (v15: ()) <- test::revoke_ap()
End:
  Goto(blk3, {v28 -> v1})

blk3:
Statements:
  (v17: core::felt252) <- core::felt252_add(v1, v0)
End:
  Return(v17)

//! > local_variables
v28

//! > ==========================================================================

//! > Remapping - fallthrough without incoming gotos

//! > test_runner_name
check_find_local_variables

//! > function_name
foo

//! > function_code
fn foo(x: felt252) -> felt252 {
    let y = bar(x + 1);
    revoke_ap();
    y
}

//! > module_code
#[inline(always)]
fn bar(x: felt252) -> felt252 {
    x
}

// Revokes ap since this function is recursive.
fn revoke_ap() {
  revoke_ap()
}

//! > lowering_format
Parameters: v0: core::felt252
blk0 (root):
Statements:
  (v1: core::felt252) <- 1u
  (v5: core::felt252) <- core::felt252_add(v0, v1)
  (v4: ()) <- test::revoke_ap()
End:
  Return(v5)

//! > local_variables
v5
